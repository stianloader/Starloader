plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "6.1.0"
}

group 'de.geolykt'
def archivesBaseName = 'starloader'
version '2.0.0'

// Our classloader require Java9+ in order to work, but since noone should be using Java 9 and 10, it was bumped all the way to Java 11
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '11'

repositories {
    mavenCentral()
    maven {
        name 'sponge'
        url 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'Geolykt'
        url = 'https://geolykt.de/maven/'
    }
}

dependencies {

    // I am very well aware that this is just a reference implementation, but it has served me well in the past; so I prefer it over any other implementation
    api 'org.json:json:20210307'

    // Let's use a library that's over 2 and a half decades old because why not?
    // However it is pretty much unmaintained by now
    // so I may need to change it out with something a bit more conventional to use with Java 11
    // https://mvnrepository.com/artifact/tablelayout/TableLayout
    api group: 'tablelayout', name: 'TableLayout', version: '20050920'

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    api group: 'org.jetbrains', name: 'annotations', version: '20.1.0'

    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    api 'com.google.code.gson:gson:2.8.6'

    // Required for mixins
    api 'com.google.guava:guava:21.0'

    // Code modification
    api "org.ow2.asm:asm:9.1"
    api "org.ow2.asm:asm-tree:9.1"
    api "org.ow2.asm:asm-analysis:9.1"
    api "org.ow2.asm:asm-util:9.1"
    api "org.ow2.asm:asm-commons:9.1"
    api "org.spongepowered:mixin:0.8.3-SNAPSHOT"
    api "de.geolykt.starloader:access-widener:2.0.0"

    // Logging
    // SLF4J is the base logger for most libraries, therefore we should hook it into log4j2.
    api 'org.apache.logging.log4j:log4j-core:2.14.1'
    api 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.1'

    api("com.heretere.hch:hch-json:1.0.0") { // config handler
        exclude group: 'com.google.code.gson' // We already import gson manually
    }
    api("com.heretere.hch:hch-yaml:1.0.0") { // the yaml version
        exclude group: 'com.google.code.gson'
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'de.geolykt.starloader.launcher.Launcher'
    }
}

shadowJar {
    mergeServiceFiles()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version '1.1.0'

            from components['java']

            artifact sourcesJar
            artifact javadocJar
        }

        // Also publish a snapshot so people can use the latest version if they wish
        snapshot(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version '1.1.0-SNAPSHOT'

            from components['java']

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        mavenLocal()
    }
}
